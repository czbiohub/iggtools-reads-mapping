rule _aligns:
    input:
        PROJ_DIR + "/5_nucmer/DONE-show-aligns"


#https://github.com/zjshi/snpMLST/blob/master/snp_mlst/snps_io/call_snps.py
#https://github.com/zjshi/snpMLST/blob/master/snp_mlst/snps_io/gen_msa.py
rule run_nucmer:
    input:
        sim = PROJ_DIR + "/genome_under_investigation.fna",
        rep = PROJ_DIR + "/uhgg_rep.fna",
    output:
        PROJ_DIR + "/5_nucmer/" + str(config["species_id"]) + ".delta",
        PROJ_DIR + "/5_nucmer/" + str(config["species_id"]) + ".coords",
        PROJ_DIR + "/5_nucmer/" + str(config["species_id"]) + ".snps",
    params:
        spid = config["species_id"],
        spid_dir = PROJ_DIR + "/5_nucmer",
        mincluster = 100, ## can't recall why exactly we used -c 100, probably need to convert to default values.
        min_coords_pid_R = LIB_DIR + "/auto_min_pid_by_delta.R",
    threads:
        2
    shell:
        """
        cd {params.spid_dir}
        nucmer --mum -c {params.mincluster} -p {params.spid}.raw {input.rep} {input.sim}

        delta-filter -q -r {params.spid}.raw.delta > {params.spid}.temp.delta
        show-coords -r -c -l {params.spid}.temp.delta > {params.spid}.temp.coords

        # filter coords by pident
        min_pid_by_delta=`Rscript {params.min_coords_pid_R} {params.spid}.temp.coords {params.spid}.coords.hist.pdf`

        delta-filter -q -r -i ${{min_pid_by_delta}} {params.spid}.temp.delta > {params.spid}.delta

        show-coords -r -c -l {params.spid}.delta > {params.spid}.coords
        show-snps -C -r -T -H {params.spid}.delta > {params.spid}.snps
        """


rule show_aligns:
    input:
        delta = PROJ_DIR + "/5_nucmer/" + str(config["species_id"]) + ".delta",
        coords = PROJ_DIR + "/5_nucmer/" + str(config["species_id"]) + ".coords",
    output:
        PROJ_DIR + "/5_nucmer/DONE-show-aligns"
    params:
        spid = config["species_id"],
        spid_dir = PROJ_DIR + "/5_nucmer",
        show_align_sh = LIB_DIR + "/run_show_aligns.sh"
    threads:
        12
    shell:
        """
        awk -F"|" '{{print $7}}' {input.coords} | sed -e 's/^ *//' | awk '{{print $1, $2}}' | grep UHGG | sort -u -k1,2 | xargs -Ixx -P {threads} bash -c "bash {params.show_align_sh} {input.delta} xx"

        touch {output}
        """


rule _sites:
    input:
        PROJ_DIR + "/5_nucmer/" + str(config["species_id"]) + "_aligned_sites.tsv"


rule generate_aligned_sites:
    input:
        PROJ_DIR + "/5_nucmer/" + str(config["species_id"]) + ".snps",
        PROJ_DIR + "/5_nucmer/" + str(config["species_id"]) + ".coords",
        PROJ_DIR + "/genome_under_investigation.fna",
        PROJ_DIR + "/uhgg_rep.fna",
        PROJ_DIR + "/5_nucmer/DONE-show-aligns"
    output:
        PROJ_DIR + "/5_nucmer/" + str(config["species_id"]) + "_aligned_sites.tsv"
    params:
        gen_sites_R = LIB_DIR + "/aligned_sites_from_alns.R",
    threads:
        2
    shell:
        """
        Rscript {params.gen_sites_R} {PROJ_DIR} {config[species_id]}
        """
